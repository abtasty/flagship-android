apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'flagship-publishing.gradle'

def ABTastySDKVersion = '2.0.0' ///DEFINE HERE THE ABTASTY SDK VERSION
def ABTastySDKVersionCode = 9 ///DEFINE HERE THE ABTASTY SDK VERSION CODE

android {
    publishNonDefault true
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 9
        versionName "2.0.0"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true

    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        all {
            buildConfigField "String", "FLAGSHIP_VERSION_NAME", "\"${ABTastySDKVersion}\""
            buildConfigField "int", "FLAGSHIP_VERSION_CODE", "${ABTastySDKVersionCode}"
            resValue("integer", "ABTastyVersionCode", "${ABTastySDKVersionCode}")
            resValue("string", "ABTastyVersion", "\"${ABTastySDKVersion}\"")
        }
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.name == android.buildTypes.release.name)
                outputFileName = "flagship-android-${ABTastySDKVersion}.aar"
        }
    }
}



androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'org.robolectric:robolectric:4.4-beta-1'
    testImplementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"


    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'androidx.room:room-runtime:2.2.5'


    kapt 'androidx.room:room-compiler:2.2.5'
    kapt "android.arch.persistence.room:compiler:2.2.4"

}


repositories {
    mavenCentral()
}
