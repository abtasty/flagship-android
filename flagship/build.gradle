apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'flagship-publishing.gradle'
apply from: 'jacoco.gradle'

android {
    publishNonDefault true
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode flagship_version_code
        versionName flagship_version_name

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        all {
            buildConfigField "String", "FLAGSHIP_VERSION_NAME", "\"${flagship_version_name}\""
            buildConfigField "int", "FLAGSHIP_VERSION_CODE", "${flagship_version_code}"
            resValue("integer", "ABTastyVersionCode", "${flagship_version_code}")
            resValue("string", "ABTastyVersion", "\"${flagship_version_name}\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '29.0.3'

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.name == android.buildTypes.release.name)
                outputFileName = "flagship-android-${flagship_version_name}.aar"
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'androidx.room:room-runtime:2.2.6'

    kapt 'androidx.room:room-compiler:2.2.6'
    kapt "android.arch.persistence.room:compiler:2.2.4"

}

repositories {
    mavenCentral()
}
